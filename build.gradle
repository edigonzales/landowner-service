plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
}

group = 'ch.so.agi.landowner'
version = '0.0.1-SNAPSHOT'

ext.jaxwsSourceDir = "${buildDir}/generated/sources/jaxws"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    jaxws
}

repositories {
    mavenCentral()
}

task wsimport {
    description = 'Generate classes from wsdl using wsimport'

    doLast {
        System.setProperty('javax.xml.accessExternalSchema', 'all')
        project.mkdir(jaxwsSourceDir)
        ant {
            taskdef(name: 'wsimport',
                    classname: 'com.sun.tools.ws.ant.WsImport',
                    classpath: configurations.jaxws.asPath
            )
            wsimport(
                    keep: true,
                    destdir: jaxwsSourceDir,
                    extension: "true",
                    verbose: true,
                    wsdl: "https://terravis.so.ch/gbdbs/gbdbs?wsdl",
                    xnocompile: true//,
                    /*package: "ch.terravis.gbdbs.wsdl"*/) { // viele Namenskonflikte
                binding(dir:"src/main/xjb", includes:"bindings.xjb") // kommasepariert
                xjcarg(value: "-XautoNameResolution")
                xjcarg(value: "-Xannotate")
            }
        }
    }
}

sourceSets {
    main {
        java.srcDirs += jaxwsSourceDir
    }
}

compileJava {
    dependsOn wsimport
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'org.springframework:spring-oxm'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    
    jaxws 'com.sun.xml.ws:jaxws-tools:3.0.0',
            'jakarta.xml.ws:jakarta.xml.ws-api:3.0.0',
            'jakarta.xml.bind:jakarta.xml.bind-api:3.0.0',
            'jakarta.activation:jakarta.activation-api:2.0.0',
            'com.sun.xml.ws:jaxws-rt:3.0.0',
            'org.jvnet.jaxb:jaxb-plugin-annotate:3.0.1'
}

tasks.named('test') {
    useJUnitPlatform()
}